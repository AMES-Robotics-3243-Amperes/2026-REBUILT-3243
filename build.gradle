plugins {
  id "java"
  id "edu.wpi.first.GradleRIO" version "2026.2.1"
  id "com.peterabeles.gversion" version "1.10"
  id "com.diffplug.spotless" version "8.1.0"
}

java {
  sourceCompatibility = JavaVersion.VERSION_17
  targetCompatibility = JavaVersion.VERSION_17
}

def ROBOT_MAIN_CLASS = "frc.robot.Main"

deploy {
  targets {
    roborio(getTargetTypeClass('RoboRIO')) {
      team = project.frc.getTeamNumber()
      debug = project.frc.getDebugOrDefault(false)

      artifacts {
        // First part is artifact name, 2nd is artifact type
        // getTargetTypeClass is a shortcut to get the class type using a string

        frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
          jvmArgs.add("-XX:+UnlockExperimentalVMOptions")
          jvmArgs.add("-XX:GCTimeRatio=5")
          jvmArgs.add("-XX:+UseSerialGC")
          jvmArgs.add("-XX:MaxGCPauseMillis=50")

          // Only enable on the RIO 2
          final MAX_JAVA_HEAP_SIZE_MB = 100;
          jvmArgs.add("-Xmx" + MAX_JAVA_HEAP_SIZE_MB + "M")
          jvmArgs.add("-Xms" + MAX_JAVA_HEAP_SIZE_MB + "M")
          jvmArgs.add("-XX:+AlwaysPreTouch")
        }

        // Static files artifact
        frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
          files = project.fileTree('src/main/deploy')
          directory = '/home/lvuser/deploy'
          deleteOldFiles = true
        }
      }
    }
  }
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

wpi.java.debugJni = false

def includeDesktopSupport = true

dependencies {
  annotationProcessor wpi.java.deps.wpilibAnnotations()
  implementation wpi.java.deps.wpilib()
  implementation wpi.java.vendor.java()

  roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
  roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

  roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
  roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

  nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
  nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
  simulationDebug wpi.sim.enableDebug()

  nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
  nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
  simulationRelease wpi.sim.enableRelease()

  testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  def akitJson = new groovy.json.JsonSlurper().parseText(new File(projectDir.getAbsolutePath() + "/vendordeps/AdvantageKit.json").text)
  annotationProcessor "org.littletonrobotics.akit:akit-autolog:$akitJson.version"
}

test {
  useJUnitPlatform()
  systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

// Simulation configuration (e.g. environment variables).
wpi.sim.addGui().defaultEnabled = true
wpi.sim.addDriverstation()

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
  from {
    configurations.runtimeClasspath.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  }
  from sourceSets.main.allSource
  manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// Configure jar and deploy tasks
deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

// Configure string concat to always inline compile
tasks.withType(JavaCompile) {
  options.compilerArgs.add '-XDstringConcat=inline'
}

gversion {
  srcDir       = "src/main/java/"
  classPackage = "frc.robot.constants"
  className    = "GitConstants"
  dateFormat   = "yyyy-MM-dd HH:mm:ss z"
  timeZone     = "America/Denver"
  debug        = false
  language     = "java"
  explicitType = true
  indent       = "  "
  annotate     = false
}

spotless {
  java {
    importOrder()
    removeUnusedImports()

    cleanthat().addMutator("SafeAndConsensual")

    trimTrailingWhitespace()
    leadingTabsToSpaces(2)
    endWithNewline()

    googleJavaFormat().reflowLongStrings(true).formatJavadoc(true).reorderImports(false)

    formatAnnotations()
  }

  groovyGradle {
    target '*.gradle'
    greclipse()

    trimTrailingWhitespace()
    leadingTabsToSpaces(2)
    endWithNewline()
  }
}

task(replayWatch, type: JavaExec) {
  mainClass = "org.littletonrobotics.junction.ReplayWatch"
  classpath = sourceSets.main.runtimeClasspath
}

// Create commit with working changes on event branches
task(commitIfEventDeploy) {
  doLast {
    if (project.gradle.startParameter.taskNames.any({ it.toLowerCase().contains("deploy") })) {
      def branchPrefix = "event"
      def branch = 'git branch --show-current'.execute().text.trim()
      def commitMessage = "Update at '${new Date().toString()}'"

      if (branch.startsWith(branchPrefix)) {
        exec {
          workingDir(projectDir)
          executable 'git'
          args 'add', '-A'
        }
        exec {
          workingDir(projectDir)
          executable 'git'
          args 'commit', '-m', commitMessage
          ignoreExitValue = true
        }

        println "Committed to branch: '$branch'"
        println "Commit message: '$commitMessage'"
      } else {
        println "Not on an event branch, skipping commit"
      }
    } else {
      println "Not running deploy task, skipping commit"
    }
  }
}

project.compileJava.dependsOn(spotlessApply)
spotlessApply.dependsOn(createVersionFile)
createVersionFile.dependsOn(commitIfEventDeploy)
